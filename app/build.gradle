apply plugin: "com.android.application"

apply plugin: "kotlin-android"

apply plugin: "kotlin-android-extensions"

apply plugin: "kotlin-kapt"

android {
    buildToolsVersion "29.0.3"

    compileSdkVersion rootProject.compile_sdk_version

    def versionMajor = 0
    def versionMinor = 0
    def versionPatch = 1

    def versionNumber = 1

    defaultConfig {
        applicationId "com.albuquerque.moviecatalog"
        minSdkVersion rootProject.min_sdk_version
        targetSdkVersion rootProject.target_sdk_version
        versionCode versionNumber
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            keyAlias "moviecatalog_key"
            keyPassword "u86txAvyUJfFpKNu"
            storeFile file("../moviecatalog_keystore.jks")
            storePassword "jTjvDXF85LgT3gXX"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            def prename = outputFile.name.substring(0, outputFile.name.length() - 4)

            if (outputFile != null && outputFile.name.endsWith('.apk') && !outputFile.name.endsWith('debug.apk')) {
                def date = new Date().format('yyyyMMddHHmmss')
                outputFileName = "moviecatalog-${prename}-${versionMajor}.${versionMinor}.${versionPatch}-${versionNumber}-${date}.apk"
            }
        }
    }

    bundle {
        density {
            // Different APKs are generated for devices with different screen densities; true by default.
            enableSplit true
        }
        abi {
            // Different APKs are generated for devices with different CPU architectures; true by default.
            enableSplit true
        }
        language {
            // This is disabled so that the App Bundle does NOT split the APK for each language. same APK for all languages.
            enableSplit false
        }
    }

    buildTypes {

        debug {
            debuggable true
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.config
            resValue "string", "app_name", "MovieCatalog Debug"
        }

        alpha {
            debuggable true
            applicationIdSuffix ".alpha"
            signingConfig signingConfigs.config
            resValue "string", "app_name", "MovieCatalog Alpha"
        }

        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding { enabled = true }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

dependencies(kotlin_dependencies)
dependencies(android_dependencies)
dependencies(room_dependencies)
dependencies(lifecycle_dependencies)
dependencies(retrofit_dependencies)
dependencies(rx_dependencies)
dependencies(debug_dependencies)
dependencies(utils_dependencies)

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:+'
}

dependencies {
    // Recommended: Add the Firebase SDK for Google Analytics.
    implementation 'com.google.firebase:firebase-analytics:17.4.0'

    // Add the Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'
    implementation 'com.google.firebase:firebase-perf:19.0.7'

    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.firebase.firebase-perf'
}
